{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Determine job-finding rates and stocks\n",
    "The job-finding rates are functions of the surplus, $\\mathcal J(\\text{surplus})=mB_2\\times\\text{surplus}^{1/\\alpha}$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function JFR(surplus::Float64, C::Union{Country1,Country2,Country1A,Country2A})\n",
    "    C.m * C.B2 * surplus^(1/C.α)\n",
    "    #C.m * C.B2 * max(surplus,0)^(1/C.α)\n",
    "end\n",
    "\n",
    "function JFR(surplus::Vector{Float64}, C::Union{Country1,Country2,Country1A,Country2A})\n",
    "    [C.m * C.B2 * S^(1/C.α) for S in surplus]\n",
    "    #[C.m * C.B2 * max(S,0)^(1/C.α) for S in surplus]\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 1) Stocks in a world with unconditional policy\n",
    "Here, we have the counterfactual dynamics in presence of the policy when everyone is eligible irrespective of their age. \n",
    "As with the surpluses, we consider three stocks: \n",
    "1. the number of unemployed workers who receive assistance benefits ($b_0$ below $A$ and $b_1$ after),\n",
    "2. the number of unemployed workers who receive UI benefits for a short period,\n",
    "3. the number of workers who receive UI benefits for a long period.\n",
    "\n",
    "In this world, no one is in state 2 (unless there is an initial condition).\n",
    "The dynamics are governed by:\n",
    "$$\n",
    "\\begin{align*}\n",
    "    & \\dot{u_1} = q(1-\\chi)(1-u_1-u_3)+ \\lambda_1 u_3 -\\mathcal J(\\Sigma_1) u_1,\\\\\n",
    "    & u_2 = 0,\\\\\n",
    "    & \\dot{u_3} = q\\chi(1-u_1-u_3) -(\\lambda_1+\\mathcal J(\\Sigma_3)) u_3.\n",
    "\\end{align*}\n",
    "$$\n",
    "\n",
    "For the first policy, the initial conditions at age $A_{min}$ are given in the Country object, $(u_1,u_3)=uinit$. \n",
    "\n",
    "For the second policy, the dynamics start at the steady state (when $\\chi=1$):\n",
    "$$\n",
    "\\begin{align*}\n",
    "    & u_1 = \\frac{q \\lambda_1}{q \\lambda_1+\\mathcal J(\\Sigma_1).(q +\\lambda_1+\\mathcal J(\\Sigma_3))},\\\\\n",
    "    & u_3 = \\frac{\\mathcal J(\\Sigma_1)}{\\lambda_1} u_1,\n",
    "\\end{align*}\n",
    "$$\n",
    "or\n",
    "\\begin{align*}\n",
    "    & u_1 = \\frac{q \\lambda_1+q(1-\\chi)\\mathcal J(\\Sigma_3)}\n",
    "    {\\mathcal J(\\Sigma_1).(q+\\mathcal J(\\Sigma_3))+\\lambda_1.(q+\\mathcal J(\\Sigma_1))+q(1-\\chi).(\\mathcal J(\\Sigma_3)-\\mathcal J(\\Sigma_1))},\\\\\n",
    "    & u_3 = \\frac{q\\chi\\mathcal J(\\Sigma_1)}\n",
    "    {\\mathcal J(\\Sigma_1).(q+\\mathcal J(\\Sigma_3))+\\lambda_1.(q+\\mathcal J(\\Sigma_1))+q(1-\\chi).(\\mathcal J(\\Sigma_3)-\\mathcal J(\\Sigma_1))}.\n",
    "\\end{align*}\n",
    "or approximated at age $A_{min}$ in the life-cycle model  (see code). "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function ODE_u_counterwith!(du::Vector, u::Vector, V::Surplus, C::Country1)\n",
    "    du[1] = C.λ * u[2] - JFR(V.Σ[1],C) * u[1]\n",
    "    du[2] =  C.q * (1-u[1]-u[2]) - ( JFR(V.Σ[2],C) + C.λ) * u[2] #/!\\ u[2] is u_3, note that Σ_2=Σ_3\n",
    "end\n",
    "\n",
    "function find_u_counterwith(V::Surplus, C::Country1)\n",
    "    tspan = (C.Amin,C.Amax)\n",
    "    prob = ODEProblem((du,u,p,t)->ODE_u_counterwith!(du,u,V,C), C.uinit,tspan)\n",
    "    sol = solve(prob) \n",
    "    return sol\n",
    "end\n",
    "\n",
    "function find_u_counterwith(V::Surplus, C::Country2)\n",
    "     ## then start with steady-state condition at Amin of ODE_u_after\n",
    "    u1 = C.q * C.λ1 / (C.q * C.λ1 + JFR(V.Σ[1],C)*(C.q + JFR(V.Σ[3],C)+C.λ1))\n",
    "    u3 = u1 *  JFR(V.Σ[1],C) / C.λ1\n",
    "    return [u1, u3]\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "## life-cycle policy 1\n",
    "function ODE_u_counterwith!(du::Vector, u::Vector, a::Float64, V::Surplus, C::Country1A)\n",
    "    q = qaux(a, C)\n",
    "    Σ = V.Σ(a)\n",
    "    du[1] = C.λ * u[2] - JFR(Σ[1],C) * u[1]\n",
    "    du[2] =  q * (1-u[1]-u[2]) - ( JFR(Σ[2],C) + C.λ) * u[2] #/!\\ u[2] is u_3\n",
    "end\n",
    "\n",
    "function find_u_counterwith(V::Surplus, C::Country1A)\n",
    "    tspan = (C.Amin,C.Amax)\n",
    "    prob = ODEProblem((du,u,p,a)->ODE_u_counterwith!(du,u,a,V,C), C.uinit, tspan)\n",
    "    sol = solve(prob) \n",
    "    return sol\n",
    "end\n",
    "\n",
    "## life-cycle policy 2\n",
    "function ODE_u_counterwith!(du::Vector, u::Vector, a::Float64, V::Surplus, C::Country2A)\n",
    "    q = qaux(a, C)\n",
    "    Σ = V.Σ(a)\n",
    "    du[1] = q * (1-C.χ) * (1-u[1]-u[2]) + C.λ1 * u[2] - JFR(Σ[1],C) * u[1]\n",
    "    du[2] =  q * C.χ * (1-u[1]-u[2]) - ( JFR(Σ[3],C) + C.λ1) * u[2] #/!\\ u[2] is u_3\n",
    "end\n",
    "\n",
    "function init_counterwith(V::Surplus, C::Country2A)\n",
    "    q = qaux(C.Amin, C)\n",
    "    Σ = V.Σ(C.Amin)\n",
    "  #=  u1 = q * C.λmin / (q * C.λmin + JFR(Σ[1],C)*(q + JFR(Σ[3],C)+C.λmin))\n",
    "    u3 = u1 *  JFR(Σ[1],C) / C.λmin =# #for χ=1\n",
    "    den = JFR(Σ[1],C) * (q + JFR(Σ[3],C)) + C.λ1 * (q + JFR(Σ[1],C)) + q * (1-C.χ) * (JFR(Σ[3],C)-JFR(Σ[1],C))\n",
    "    u1 = q * (C.λ1 + (1-C.χ) * JFR(Σ[3],C)) / den\n",
    "    u3 = q * C.χ * JFR(Σ[1],C) / den\n",
    "    return [u1, u3]\n",
    "end\n",
    "\n",
    "function find_u_counterwith(V::Surplus, C::Country2A)\n",
    "    tspan = (C.Amin,C.Amax)\n",
    "    uinit = init_counterwith(V, C)\n",
    "    prob = ODEProblem((du,u,p,a)->ODE_u_counterwith!(du,u,a,V,C), uinit, tspan)\n",
    "    sol = solve(prob) \n",
    "    return sol\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 2) Stocks in a world without the policy\n",
    "In this world, no one is in state 3 (unless there is an initial condition).\n",
    "The dynamics are governed by:\n",
    "$$\n",
    "\\begin{align*}\n",
    "    & \\dot{u_1} = q(1-\\chi)(1-u_1-u_2)+\\lambda_0 u_3 -\\mathcal J(\\Psi_1) u_1,\\\\\n",
    "    & \\dot{u_2} = q\\chi(1-u_1-u_2) -(\\lambda_0+\\mathcal J(\\Psi_2)) u_2.\n",
    "\\end{align*}\n",
    "$$\n",
    "For the first policy, the initial conditions at age $A_{min}$ are given in the Country object, $(u_1,u_2)=uinit$. \n",
    "For the second policy, the dynamics start at the steady state when $\\chi=1$\n",
    "$$\n",
    "\\begin{align*}\n",
    "    & u_1 = \\frac{q \\lambda_0}{(q \\lambda_{0}+\\mathcal J(\\Psi_1))(q +\\lambda_0+\\mathcal J(\\Psi_2))},\\\\\n",
    "    & u_2 = \\frac{\\mathcal J(\\Psi_1)}{\\lambda_0} u_1,\n",
    "\\end{align*}\n",
    "$$\n",
    "or approximated at age Amin (see code). "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "##counterfactual dynamics without the policy\n",
    "function ODE_u_counterwithout!(du::Vector, u::Vector, V::Surplus, C::Country1)\n",
    "    du[1] = C.λ * u[2] - JFR(V.Ψ[1],C) * u[1]\n",
    "    du[2] = C.q * (1-u[1]-u[2]) - (JFR(V.Ψ[2],C) + C.λ) * u[2]\n",
    "end\n",
    "\n",
    "function find_u_counterwithout(V::Surplus, C::Country1)\n",
    "    tspan = (C.Amin,C.Amax)\n",
    "    prob = ODEProblem((du,u,p,t)->ODE_u_counterwithout!(du,u,V,C), C.uinit,tspan)\n",
    "    sol = solve(prob) \n",
    "    return sol\n",
    "end\n",
    "\n",
    "function find_u_counterwithout(V::Surplus, C::Country2)\n",
    "    u1 = C.q * C.λ0 / (C.q * C.λ0 + JFR(V.Ψ[1],C)*(C.q + JFR(V.Ψ[2],C)+C.λ0))\n",
    "    u2 = u1 * JFR(V.Ψ[1],C) / C.λ0\n",
    "    return [u1, u2]\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "## life-cycle policy 1\n",
    "function ODE_u_counterwithout!(du::Vector, u::Vector, a::Float64, V::Surplus, C::Country1A)\n",
    "    q = qaux(a, C)\n",
    "    Ψ = V.Ψ(a)\n",
    "    du[1] = C.λ * u[2] - JFR(Ψ[1],C) * u[1]\n",
    "    du[2] = q * (1-u[1]-u[2]) - (JFR(Ψ[2],C) + C.λ) * u[2]\n",
    "end\n",
    "\n",
    "function find_u_counterwithout(V::Surplus, C::Country1A)\n",
    "    tspan = (C.Amin,C.Amax)\n",
    "    prob = ODEProblem((du,u,p,a)->ODE_u_counterwithout!(du,u,a,V,C), C.uinit, tspan)\n",
    "    sol = solve(prob) \n",
    "    return sol\n",
    "end\n",
    "\n",
    "## life-cycle policy 2\n",
    "function ODE_u_counterwithout!(du::Vector, u::Vector, a::Float64, V::Surplus, C::Country2A)\n",
    "    q = qaux(a, C)\n",
    "    Ψ = V.Ψ(a)\n",
    "    du[1] = q * (1-C.χ) * (1-u[1]-u[2]) + C.λ0 * u[2] - JFR(Ψ[1],C) * u[1]\n",
    "    du[2] = q * C.χ * (1-u[1]-u[2]) - (JFR(Ψ[2],C) + C.λ0) * u[2]\n",
    "end\n",
    "\n",
    "function init_counterwithout(V::Surplus, C::Country2A)\n",
    "    q = qaux(C.Amin, C)\n",
    "    Ψ = V.Ψ(C.Amin)\n",
    "    #=u1 = q * C.λmax / (q * C.λmax + JFR(Ψ[1],C)*(q + JFR(Ψ[2],C)+C.λmax))\n",
    "    u2 = u1 * JFR(Ψ[1],C) / C.λmax=# #for χ=1\n",
    "    den = JFR(Ψ[1],C) * (q + JFR(Ψ[2],C)) + C.λ0 * (q + JFR(Ψ[1],C)) + q * (1-C.χ) * (JFR(Ψ[2],C)-JFR(Ψ[1],C))\n",
    "    u1 = q * (C.λ0 + (1-C.χ) * JFR(Ψ[2],C)) / den\n",
    "    u2 = q * C.χ * JFR(Ψ[1],C) / den\n",
    "    return [u1, u2]\n",
    "end\n",
    "\n",
    "function find_u_counterwithout(V::Surplus, C::Country2A)\n",
    "    tspan = (C.Amin,C.Amax)\n",
    "    uinit = init_counterwithout(V, C)\n",
    "    prob = ODEProblem((du,u,p,a)->ODE_u_counterwithout!(du,u,a,V,C), uinit, tspan)\n",
    "    sol = solve(prob) \n",
    "    return sol\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 3) Stocks with age-conditional policy\n",
    "\n",
    "Before age $A$, we have the dynamics:\n",
    "$$\n",
    "\\begin{align*}\n",
    "    & \\dot{u_1} = q(1-\\chi)(1-u_1-u_2)+\\lambda_0 u_2 -\\mathcal J(S_1(a)) u_1,\\\\\n",
    "    & \\dot{u_2} = q\\chi(1-u_1-u_2) -(\\lambda_0+\\mathcal J(S_2(a)) u_2,\\\\\n",
    "    & u_3 = 0.\n",
    "\\end{align*}\n",
    "$$\n",
    "\n",
    "For the first policy, the initial conditions at age $A_{min}$ are given in the Country object, $(u_1,u_2)=uinit$, and always $\\chi=1$. \n",
    "For the second policy, the economy starts at the initial state of the counterfactual dynamics without policy, corresponding to a myopic steady state.\n",
    "\n",
    "After age $A$, the dynamics are:\n",
    "$$\n",
    "\\begin{align*}\n",
    "    & \\dot{u_1} = q(1-\\chi)(1-u_1-u_2-u_3)+\\lambda_0u_2 + \\lambda_1u_3 -\\mathcal J(\\Sigma_1) u_1,\\\\\n",
    "    & \\dot{u_2} = -(\\lambda_0+\\mathcal J(\\Sigma_2)) u_2,\\\\\n",
    "    & \\dot{u_3} = q\\chi(1-u_1-u_2-u_3) -(\\lambda_1+\\mathcal J(\\Sigma_3)) u_3.\n",
    "\\end{align*}\n",
    "$$\n",
    "with initial conditions at age $A$ given by the solution of the previous system.\n",
    "\n",
    "For the first policy, we ignore state 3 and $\\chi=1$ so the system is simply\n",
    "$$\n",
    "\\begin{align*}\n",
    "    & \\dot{u_1} = \\lambda u_2  -\\mathcal J(\\Sigma_1) u_1,\\\\\n",
    "    & \\dot{u_2} = q(1-u_1-u_2) -(\\lambda+\\mathcal J(\\Sigma_2)) u_2.\n",
    "\\end{align*}\n",
    "$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "## first policy\n",
    "function ODE_u_realbefore(du::Vector, u::Vector, a::Float64, V::Surplus, C::Country1)\n",
    "    du[1] = C.λ * u[2] - JFR(V.S(a)[1],C) * u[1]\n",
    "    du[2] = C.q * (1-u[1]-u[2]) - (JFR(V.S(a)[2],C) + C.λ) * u[2]\n",
    "end\n",
    "\n",
    "function find_u_realbefore(V::Surplus, C::Country1)\n",
    "    tspan = (C.Amin,C.A)\n",
    "    prob = ODEProblem((du,u,p,a)->ODE_u_realbefore(du,u,a,V,C), C.uinit,tspan)\n",
    "    sol_before = solve(prob)\n",
    "    return sol_before\n",
    "end\n",
    "\n",
    "function ODE_u_realafter(du::Vector, u::Vector, V::Surplus, C::Country1)\n",
    "    du[1] = C.λ * u[2] - JFR(V.Σ[1],C) * u[1]\n",
    "    du[2] =  C.q * (1-u[1]-u[2]) - ( JFR(V.Σ[2],C) + C.λ) * u[2]  ## note Σ_2=Σ_3\n",
    "end\n",
    "\n",
    "function find_u_realafter(uinitA::Vector, V::Surplus, C::Country1)\n",
    "    tspan = (C.A,C.Amax)\n",
    "    prob = ODEProblem((du,u,p,t)->ODE_u_realafter(du,u,V,C),uinitA,tspan)\n",
    "    sol_after = solve(prob)\n",
    "    return sol_after\n",
    "end\n",
    "\n",
    "function find_u_real(V::Surplus, C::Country1)\n",
    "    ureal_bef = find_u_realbefore(V, C)\n",
    "    uinitA = ureal_bef(C.A)\n",
    "    ureal_aft = find_u_realafter(uinitA, V, C)\n",
    "    function ureal(a)\n",
    "        if a < C.A\n",
    "            ureal_bef(a)\n",
    "        else\n",
    "            ureal_aft(a)\n",
    "        end\n",
    "    end\n",
    "    return ureal\n",
    "end\n",
    "\n",
    "\n",
    "## second policy\n",
    "function ODE_u_realbefore(du::Vector, u::Vector, a::Float64, V::Surplus, C::Country2)\n",
    "    du[1] = C.λ0 * u[2] - JFR(V.S(a)[1],C) * u[1]\n",
    "    du[2] = C.q * (1-u[1]-u[2]) - (JFR(V.S(a)[2],C) + C.λ0) * u[2]\n",
    "end\n",
    "\n",
    "function find_u_realbefore(V::Surplus, C::Country2)\n",
    "    u1 = C.q * C.λ0 / (C.q * C.λ0 + JFR(V.Ψ[1],C)*(C.q + JFR(V.Ψ[2],C)+C.λ0))\n",
    "    u2 = u1 * JFR(V.Ψ[1],C) / C.λ0\n",
    "    uinit = [u1, u2]\n",
    "    tspan = (C.Amin,C.A)\n",
    "    prob = ODEProblem((du,u,p,a)->ODE_u_realbefore(du,u,a,V,C),uinit,tspan)\n",
    "    sol_before = solve(prob) \n",
    "    return a -> vcat(sol_before(a), 0.)\n",
    "end\n",
    "\n",
    "function ODE_u_realafter(du::Vector, u::Vector, a::Float64, V::Surplus, C::Country2)\n",
    "    du[1] = C.λ0 * u[2] + C.λ1 * u[3] - JFR(V.Σ[1],C) * u[1]\n",
    "    du[2] = - (JFR(V.Σ[2],C) + C.λ0) * u[2]\n",
    "    du[3] =  C.q * (1-u[1]-u[2]-u[3]) - ( JFR(V.Σ[3],C) + C.λ1) * u[3]\n",
    "end\n",
    "\n",
    "function find_u_realafter(uinitA::Vector, V::Surplus, C::Country2)\n",
    "    tspan = (C.A,C.Amax)\n",
    "    prob = ODEProblem((du,u,p,t)->ODE_u_realafter(du,u,t,V,C),uinitA,tspan)\n",
    "    sol_after = solve(prob) \n",
    "    return a->sol_after(a)\n",
    "end\n",
    "\n",
    "function find_u_real(V::Surplus, C::Country2)\n",
    "    ureal_bef = find_u_realbefore(V, C)\n",
    "    uinitA = ureal_bef(C.A)\n",
    "    ureal_aft = find_u_realafter(uinitA, V, C)\n",
    "    function ureal(a)\n",
    "        if a<C.A\n",
    "            ureal_bef(a)\n",
    "        else\n",
    "            ureal_aft(a)\n",
    "        end\n",
    "    end\n",
    "    return ureal \n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "## life-cycle policy 1\n",
    "function ODE_u_realbefore(du::Vector, u::Vector, a::Float64, V::Surplus, C::Country1A)\n",
    "    q = qaux(a, C)\n",
    "    S = V.S(a)\n",
    "    du[1] = C.λ * u[2] - JFR(S[1],C) * u[1]\n",
    "    du[2] = q * (1-u[1]-u[2]) - (JFR(S[2],C) + C.λ) * u[2]\n",
    "end\n",
    "\n",
    "function find_u_realbefore(V::Surplus, C::Country1A)\n",
    "    tspan = (C.Amin,C.A)\n",
    "    prob = ODEProblem((du,u,p,a)->ODE_u_realbefore(du,u,a,V,C), C.uinit,tspan)\n",
    "    sol_before = solve(prob)\n",
    "    return sol_before\n",
    "end\n",
    "\n",
    "function ODE_u_realafter(du::Vector, u::Vector, a::Float64, V::Surplus, C::Country1A)\n",
    "    q = qaux(a, C)\n",
    "    Σ = V.Σ(a)\n",
    "    du[1] = C.λ * u[2] - JFR(Σ[1],C) * u[1]\n",
    "    du[2] =  q * (1-u[1]-u[2]) - ( JFR(Σ[2],C) + C.λ) * u[2]  ## note Σ_2=Σ_3\n",
    "end\n",
    "\n",
    "function find_u_realafter(uinitA::Vector, V::Surplus, C::Country1A)\n",
    "    tspan = (C.A,C.Amax)\n",
    "    prob = ODEProblem((du,u,p,a)->ODE_u_realafter(du,u,a,V,C),uinitA,tspan)\n",
    "    sol_after = solve(prob) \n",
    "    return sol_after\n",
    "end\n",
    "\n",
    "function find_u_real(V::Surplus, C::Country1A)\n",
    "    ureal_bef = find_u_realbefore(V, C)\n",
    "    uinitA = ureal_bef(C.A)\n",
    "    ureal_aft = find_u_realafter(uinitA, V, C)\n",
    "    function ureal(a)\n",
    "        if a < C.A\n",
    "            ureal_bef(a)\n",
    "        else\n",
    "            ureal_aft(a)\n",
    "        end\n",
    "    end\n",
    "    return ureal\n",
    "end\n",
    "\n",
    "\n",
    "## life-cycle policy 2\n",
    "function ODE_u_realbefore(du::Vector, u::Vector, a::Float64, V::Surplus, C::Country2A)\n",
    "    q = qaux(a, C)\n",
    "    S = V.S(a)\n",
    "    du[1] = q * (1-C.χ) * (1-u[1]-u[2]) + C.λ0 * u[2] - JFR(S[1],C) * u[1]\n",
    "    du[2] = q * C.χ * (1-u[1]-u[2]) - (JFR(S[2],C) + C.λ0) * u[2]\n",
    "end\n",
    "\n",
    "function find_u_realbefore(uinit::Vector, V::Surplus, C::Country2A)\n",
    "    tspan = (C.Amin,C.A)\n",
    "    prob = ODEProblem((du,u,p,a)->ODE_u_realbefore(du,u,a,V,C),uinit,tspan)\n",
    "    sol_before = solve(prob) \n",
    "    return a -> vcat(sol_before(a), 0.)\n",
    "end\n",
    "\n",
    "function ODE_u_realafter(du::Vector, u::Vector, a::Float64, V::Surplus, C::Country2A)\n",
    "    q = qaux(a, C)\n",
    "    Σ = V.Σ(a)\n",
    "    du[1] = q * (1-C.χ) * (1-u[1]-u[2]-u[3]) + C.λ0 * u[2] + C.λ1 * u[3] - JFR(Σ[1],C) * u[1]\n",
    "    du[2] = - (JFR(Σ[2],C) + C.λ0) * u[2]\n",
    "    du[3] =  q * C.χ * (1-u[1]-u[2]-u[3]) - ( JFR(Σ[3],C) + C.λ1) * u[3]\n",
    "end\n",
    "\n",
    "function find_u_realafter(uinitA::Vector, V::Surplus, C::Country2A)\n",
    "    tspan = (C.A,C.Amax)\n",
    "    prob = ODEProblem((du,u,p,a)->ODE_u_realafter(du,u,a,V,C),uinitA,tspan)\n",
    "    sol_after = solve(prob) \n",
    "    return a->sol_after(a)\n",
    "end\n",
    "\n",
    "function find_u_real(V::Surplus, C::Country2A)\n",
    "    uinit = init_counterwithout(V, C)\n",
    "    ureal_bef = find_u_realbefore(uinit, V, C)\n",
    "    uinitA = ureal_bef(C.A)\n",
    "    ureal_aft = find_u_realafter(uinitA, V, C)\n",
    "    function ureal(a)\n",
    "        if a<C.A\n",
    "            ureal_bef(a)\n",
    "        else\n",
    "            ureal_aft(a)\n",
    "        end\n",
    "    end\n",
    "    return ureal \n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 4) Wrap-up"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function find_Unemp(V::Surplus, C::Union{Country1, Country2, Country1A, Country2A}; \n",
    "        find_with=true, find_real=true, find_without=true)\n",
    "    with = find_with ? find_u_counterwith(V, C) : zeros(2)\n",
    "    without = find_without ? find_u_counterwithout(V, C) : zeros(2)\n",
    "    real = find_real ? find_u_real(V, C) : zeros(3)\n",
    "    return Unemp(with=with, real=real, without=without)\n",
    "end"
   ]
  }
 ],
 "metadata": {
  "@webio": {
   "lastCommId": null,
   "lastKernelId": null
  },
  "kernelspec": {
   "display_name": "Julia 1.10.0",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
